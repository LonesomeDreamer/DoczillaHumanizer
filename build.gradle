repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'http://mvn.revoltsoft.ru/' }
}

group = 'pro.doczilla'
version = '1.0'

buildDir = 'target'

apply plugin: 'base'
apply plugin: 'maven'
apply plugin: 'maven-publish'

configurations.default.attributes {
	attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, project.objects.named(LibraryElements, 'web'))
}

def windows = org.apache.tools.ant.taskdefs.condition.Os.isFamily(org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS)
ext.npmCmd = windows ? 'npm.cmd' : 'npm'

task npmInstall(type: Exec) {
	workingDir projectDir
	commandLine npmCmd, 'install'
}

task buildJs(type: Exec) {
	dependsOn npmInstall
	workingDir projectDir
	commandLine npmCmd, 'run', 'build'
}

task webZip(type: Zip) {
	dependsOn buildJs
	group 'Build'
	description 'Assemble JS archive'

	archiveName "${project.name}-${project.version}.zip"
	destinationDir project.file("${project.buildDir}/libs/")

	from(project.buildDir) {
		include 'web/pro.doczilla.linguistics.js.js'
	}
}

build.dependsOn webZip

artifacts.add('default', webZip) {
	builtBy webZip
}

publishing {
	repositories {
		mavenLocal()
	}
	publications {
		maven(MavenPublication) {
			artifact source: webZip, extension: 'zip'
		}
	}
}
